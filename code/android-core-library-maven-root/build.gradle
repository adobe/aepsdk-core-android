apply plugin: 'com.android.library'
apply plugin: 'signing'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 19
        //noinspection OldTargetApi
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.adobe.marketing.mobile'
            artifactId = 'sdk-core'
            version = coreLibraryMavenRootVersion
            pom.withXml {
                asNode().appendNode('name', mavenRepoName)
                asNode().appendNode('description', mavenRepoDescription)
                asNode().appendNode('url', 'https://aep-sdks.gitbook.io')
                def scmNode = asNode().appendNode('scm')
                scmNode.appendNode('url', 'https://github.com/Adobe-Marketing-Cloud/acp-sdks')

                def developersNode = asNode().appendNode('developers')
                def developerNode = developersNode.appendNode('developer')
                developerNode.appendNode('id', 'adobe')
                developerNode.appendNode('name', 'adobe')

                def licensesNode = asNode().appendNode('licenses')
                def licenseNode = licensesNode.appendNode('license')
                licenseNode.appendNode('name', 'Adobe Proprietary')

                def dependenciesNode = asNode().appendNode('dependencies')

                def dependencynode = dependenciesNode.appendNode('dependency')
                dependencynode.appendNode('groupId', 'com.adobe.marketing.mobile')
                dependencynode.appendNode('artifactId', 'core')
                dependencynode.appendNode('version', mavenCoreVersion)

                def singalNode = dependenciesNode.appendNode('dependency')
                singalNode.appendNode('groupId', 'com.adobe.marketing.mobile')
                singalNode.appendNode('artifactId', 'signal')
                singalNode.appendNode('version', mavenSignalVersion)

                def identityNode = dependenciesNode.appendNode('dependency')
                identityNode.appendNode('groupId', 'com.adobe.marketing.mobile')
                identityNode.appendNode('artifactId', 'identity')
                identityNode.appendNode('version', mavenIdentityVersion)

                def lifecycleNode = dependenciesNode.appendNode('dependency')
                lifecycleNode.appendNode('groupId', 'com.adobe.marketing.mobile')
                lifecycleNode.appendNode('artifactId', 'lifecycle')
                lifecycleNode.appendNode('version', mavenLifecycleVersion)
            }

            pom.setPackaging('pom')
        }
    }
    repositories {
        maven {
            name = "sonatype"
            url = version.endsWith('SNAPSHOT') ? "https://oss.sonatype.org/content/repositories/snapshots/" : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publishReleasePublicationToSonatypeRepository") }
    sign publishing.publications.release
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}
